module Anoma.Transaction.Metadata;

import Stdlib.Prelude open;
import Anoma.Transaction.Object as Transaction open using {Transaction};
import Anoma.Transaction.Preference as Preference open using {Preference; composePreferences};
import Anoma.Transaction.InformationFlow open using {InformationFlowControlPredicate};

--- The metadata that can be associated with a ;Transaction;.
--- The metadata that can used by actors to identify preferrable ;Transaction;s and specify information flow control properties.
--- NOTE: For the private testnet, this is not required yet.
type Metadata :=
  mkMetadata {
    preference : Preference;
    informationFlowControlPredicate : InformationFlowControlPredicate
  };

--- A composite structure containing a ;Transaction; with ;Metadata;
--- NOTE: For the private testnet, this is not required yet.
type TransactionWithMetadata :=
  createWithMetadata {
    transaction : Transaction;
    metadata : Metadata
  };

--- Composes two ;TransactionWithMetadata; objects.
--- NOTE: For the private testnet, this is not required yet.
--- TODO This should be an Anoma builtin. NOTE: For the private testnet, this is not required yet.
composeTransactionsWithMetadata (txwm1 txwm2 : TransactionWithMetadata) : TransactionWithMetadata :=
  let
    meta1 := TransactionWithMetadata.metadata txwm1;
    meta2 := TransactionWithMetadata.metadata txwm2;
  in createWithMetadata@{
    transaction :=
      Transaction.composeTransactions@{
        tx1 := TransactionWithMetadata.transaction txwm1;
        tx2 := TransactionWithMetadata.transaction txwm2
      };
    metadata :=
      mkMetadata@{
        preference :=
          Preference.composePreferences@{
            f1 := Metadata.preference meta1;
            f2 := Metadata.preference meta2
          };
        informationFlowControlPredicate :=
          \ {tx id :=
            Metadata.informationFlowControlPredicate meta1 tx id
              && Metadata.informationFlowControlPredicate meta2 tx id}
      }
  };

--- Verifies a ;TransactionWithMetadata;.
--- TODO This should be an Anoma builtin. NOTE: For the private testnet, this is not required yet.
verifyTransactionWithMetadata (txwm : TransactionWithMetadata) : Bool :=
  Transaction.verifyTransaction (TransactionWithMetadata.transaction txwm);
