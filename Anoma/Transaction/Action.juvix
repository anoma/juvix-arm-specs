module Anoma.Transaction.Action;

import Stdlib.Prelude open;
import Stdlib.Trait.Ord.Eq open using {fromOrdToEq};
import Data.Set as Set open using {Set};
import Anoma.Resource as Resource open;
import Anoma.Proving.Types open using {ProofRecord};
import Anoma.Proving.ProofRecordSet as ProofRecordSet open using {ProofRecordSet};
import Anoma.Transaction.AppData open using {AppData};
import Anoma.Utils open;

--- The record describing an action and defining the proof context.
--- Proofs includes resource logic and compliance proofs of created and consumed resources.
--- The latter include multiple constraints such as the
--- - non-existence of created ;Resource;s in the commitment tree
--- - existence of consumed ;Resource;s in the commitment tree
--- - non-existence of consumed ;Resource;s in the nullifier set.
type Action :=
  mkAction {
    commitments : Set Resource.Commitment;
    nullifiers : Set Resource.Nullifier;
    -- TODO: proofs is not compatible with RM instantiation.
    -- https://github.com/anoma/anoma/blob/2bd7784de9543d9cfc85ed49bd77cec549f8249c/hoon/resource-machine.hoon#L60
    proofs : ProofRecordSet;
    appData : AppData
  };

--- Verifies an ;Action; by verifying all proofs in the ;ProofRecordSet;.
verifyAction (a : Action) : Bool := NOT_REQUIRED;

module ActionInternal;
  --- Compares two ;Action; objects.
  compare (lhs rhs : Action) : Ordering :=
    let
      prod (a : Action) : _ :=
        Action.commitments a, Action.nullifiers a, Action.proofs a, Action.appData a;
    in Ord.cmp (prod lhs) (prod rhs);

  --- Implements the ;Ord; trait for ;Action;.
  instance
  Action-Ord : Ord Action := mkOrd compare;

  --- Implements the ;Eq; trait for ;Action;.
  instance
  Action-Eq : Eq Action := fromOrdToEq;
end;
