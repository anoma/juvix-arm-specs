module Anoma.Resource.Types;

import Stdlib.Prelude open;

--- A fixed-size data type encoding the reference to the resource logic function.
--- NOTE: For the private testnet, the requirement can be relaxed by allowing ;LogicRef; to be dynamically-sized.
axiom LogicRef : Type;

--- A fixed-size data type encoding the reference to the resource label field.
--- NOTE: For the private testnet, the requirement can be relaxed by allowing ;LabelRef; to be dynamically-sized.
axiom LabelRef : Type;

--- A fixed-size data type encoding the reference to the resource value field.
--- NOTE: For the private testnet, the requirement can be relaxed by allowing ;ValueRef; to be dynamically-sized.
axiom ValueRef : Type;

--- A fixed-size data type encoding the quantity of a resource.
axiom Quantity : Type;

--- A fixed-size data type encoding the public commitment to the private nullifier key.
axiom NullifierKeyCommitment : Type;

--- A fixed-size data type encoding a number to be used once ensuring that the resource commitment is unique.
--- NOTE: This should be a number having an at most negligible chance of repeating is sufficient, e.g., a pseudo-random number.
axiom Nonce : Type;

--- A fixed-size data type encoding a randomness seed.
--- NOTE: This seed provides pseudo randomness and cannot be expected to provide true randomness.
axiom RandSeed : Type;

--- A record describing a resource, the atomic unit of state in the Anoma state model.
type Resource :=
  mkResource {
    logicRef : LogicRef;
    labelRef : LabelRef;
    valueRef : ValueRef;
    quantity : Quantity;
    ephemeral : Bool;
    nullifierKeyCommitment : NullifierKeyCommitment;
    nonce : Nonce;
    randSeed : RandSeed
  };

--- A trait for transforming values of a `DataType` into values of its
--- corresponding `RefType`. Values of `RefType` must have fixed size. For the
--- private testnet, the requirement can be relaxed by allowing ;RefType; to be
--- dynamically-sized.
trait
type Ref DataType RefType := mkRef {ref : DataType -> RefType};
