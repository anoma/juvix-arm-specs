--- An instantiation of a proving System for resource logic proofs.
module Anoma.ProvingSystem.ResourceLogicProof;

import Stdlib.Prelude open;
import Data.Set as Set open using {Set};
import Anoma.Resource.Object open using {Resource};
import Anoma.Resource.Types open using {Commitment; Nullifier};
import Anoma.ProvingSystem.Types as Parametrized;

axiom Proof : Type;

axiom VerifyingKey : Type;

axiom ProvingKey : Type;

--- The reference to the ;Resource; carrying the logic function.
type Tag :=
  | Created Commitment
  | Consumed Nullifier;

--- The public inputs (Instance) of the resource logic function proof.
type Instance :=
  mkInstance {
    tag : Tag;
    commitments : Set Commitment;
    nullifiers : Set Nullifier;
    custom : InstanceCustomInputs
  };

--- Custom public inputs of the resource logic function proof being defined on the application level.
axiom InstanceCustomInputs : Type;

--- The private inputs (Witness) of the resource logic function proof.
type Witness :=
  mkWitness {
    created : Set Resource;
    consumed : Set Resource;
    custom : WitnessCustomInputs
  };

--- Custom private inputs of the resource logic function proof being defined on the application level.
axiom WitnessCustomInputs : Type;

ProofRecord : Type := Parametrized.ProofRecord Proof VerifyingKey Witness;

axiom prove : (provingKey : ProvingKey)
  -> (publicInputs : Instance)
  -> (privateInputs : Witness)
  -> Proof;

axiom verify : (proofRecord : ProofRecord) -> Bool;

instance
ResourceLogic-ProvingSystem
  : Parametrized.ProvingSystem Proof VerifyingKey ProvingKey Instance Witness :=
  Parametrized.mkProvingSystem@{
    prove;
    verify
  };
